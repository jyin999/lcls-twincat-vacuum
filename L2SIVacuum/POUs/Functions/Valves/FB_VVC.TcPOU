<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_VVC" Id="{a538d8bd-5f3e-49c2-b528-765b2211bef2}" SpecialFunc="None">
    <Declaration><![CDATA[(* This Function Block Implements Basic Functionality for Vent Valves VVC.

    This function block is also intended for the general use case of solenoid driven (non-variable) valves that lack status readbacks or position indicators.
*)
(* Note Interlock Logic is External *)
FUNCTION_BLOCK FB_VVC
VAR_IN_OUT

END_VAR
VAR_INPUT
    i_xExtILK_OK:BOOL; (*Other External Interlock, Set to True when no external interlock is required*)
    i_xOverrideMode : BOOL; (*To be linked to global override bit. This Overrides Vacuum logic only*)
END_VAR
VAR_OUTPUT
    {attribute 'pytmc' := '
    pv:
    '}
    iq_stValve : ST_VVC;
END_VAR
VAR

    tonOvrd	:	TON;
    tonDelOK : TON;
    rtOK	:	R_TRIG;

    (*IO*)
    q_xOPN_DO	AT%Q*: BOOL;

    pv_xOvrdOpn: INT;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[
iq_stValve.xOPN_OK := i_xExtILK_OK;

IF NOT iq_stValve.xOPN_OK THEN
iq_stValve.pv_xOPN_SW := FALSE;
END_IF

(* Override logic *)
(* Goal: give ability to override, but do so in a way that people won't forget it.
Solution: Override only after ten seconds of override, protect against blips,
when the valve permission goes true for more than ten seconds consistently, remove override
*)

tonDelOK(IN:=iq_stValve.xOPN_OK, PT:=T#10S);
rtOK(CLK:=tonDelOK.Q);
IF rtOK.Q THEN iq_stValve.xOvrdOpn :=FALSE; END_IF
// Release the Force Open bit when the system override is false
IF NOT (i_xOverrideMode) THEN  iq_stValve.xOvrdOpn :=FALSE; END_IF

//Override timer
tonOvrd(IN:=iq_stValve.xOvrdOpn, PT:=T#10S);

(* Here's where the valve opens *)
iq_stValve.q_xOPN_DO := (iq_stValve.pv_xOPN_SW AND iq_stValve.xOPN_OK) OR (tonOvrd.Q AND i_xOverrideMode);

(*IO Mapping*)
ACT_IO();]]></ST>
    </Implementation>
    <Action Name="ACT_IO" Id="{ffed0298-5cb5-4bca-838f-8066b1682c67}">
      <Implementation>
        <ST><![CDATA[(*inputs*)
iq_stValve.xOverrideMode := i_xOverrideMode;
(*outputs*)
q_xOPN_DO:= iq_stValve.q_xOPN_DO;]]></ST>
      </Implementation>
    </Action>
    <Method Name="M_IsClosed" Id="{01a9d73c-fa4d-4cbb-8f1d-032d6c8e6684}">
      <Declaration><![CDATA[// Method Returns True if the Valve is closed, i.e. the open digital output is not active
METHOD PUBLIC M_IsClosed : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[M_IsClosed := NOT This^.q_xOPN_DO;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_IsOpen" Id="{baefc52d-c554-4c1d-a7d8-483b83575ac0}">
      <Declaration><![CDATA[// Method Returns True if the Valve is open, i.e. the open digital output is active
METHOD PUBLIC M_IsOpen : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[M_IsOpen := This^.q_xOPN_DO;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Open" Id="{d474e7d3-5328-4b96-aefd-0d239f7cb017}">
      <Declaration><![CDATA[METHOD PUBLIC M_Open : BOOL
VAR_INPUT
    open: bool; //Set to true to open the valvue, false to close
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[This^.iq_stValve.pv_xOPN_SW := open;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Set_OPN_SW" Id="{6bb3ce16-a9f1-46bd-80bc-4a279a4b4310}">
      <Declaration><![CDATA[// Use this Methode to Open or close the valve by setting the OPN_SW to the input value.
METHOD PUBLIC M_Set_OPN_SW : BOOL
VAR_INPUT
    value:BOOL; //True to SET, False to RESET
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[This^.iq_stValve.pv_xOPN_SW := value;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>